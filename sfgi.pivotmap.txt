[LegacyColorValue = true]; 

{
Indicator   : sfgi.pivotmap
Description : Display the Pivot Map

Copyright 2016 Scottfree Gains LLC

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
}

Inputs:
	Length(50),
	MaxPivots(50),
	MinStrength(3),
	ScoreAlert(20),
	HighColor(Blue),
	LowColor(Red),
	PBWidth(2);

Variables:
	PivotCount(0),
	SPivotCount(0),
	dt(0),
	ip(0),
	ps(""),
	ph(0),
	pl(0),
	pb(0),
	Color(0),
	PHigh(0.0),
	PLow(0.0),
	PivotText(0),
	PivotScore(0.0);

Arrays:
	PivotMap[50, 4](0);

{Find Pivots}

print(date, PivotCount);
PivotCount = sfgf.pivotmap(Length, MaxPivots, PivotMap);
print(date, PivotCount);
SPivotCount = 0;
For ip = 0 To PivotCount-1 Begin
	If PivotMap[ip, 2] >= MinStrength Then
		SPivotCount = SPivotCount + 1;
End;	

{Pivot Commentary}

dt = ELDateToDateTime(Date);
CommentaryCL(NewLine,
			 NumToStr(MonthFromDateTime(dt), 0), " / ",
			 NumToStr(DayFromDateTime(dt), 0), " / ",
			 NumToStr(YearFromDateTime(dt), 0),
			 NewLine, "Total Pivots: ", NumToStr(PivotCount, 0),
			 NewLine, "Pivots (Strength ", NumToStr(MinStrength, 0), "): ",
			 NumToStr(SPivotCount, 0),
			 NewLine);
For ip = 0 To PivotCount-1 Begin
	If PivotMap[ip, 2] >= MinStrength Then Begin
		dt = ELDateToDateTime(Date[PivotMap[ip, 0]]);
		If PivotMap[ip, 1] = 1 Then ps = "High";
		If PivotMap[ip, 1] = -1 Then ps = "Low";
		CommentaryCL("   ", ps,
			 		 " Pivot at Bar ", NumToStr(PivotMap[ip, 0], 0),
			 		 " with Strength ", NumToStr(PivotMap[ip, 2], 0),
			 		 " on ", NumToStr(MonthFromDateTime(dt), 0), " / ",
			 		 NumToStr(DayFromDateTime(dt), 0), " / ",
			 		 NumToStr(YearFromDateTime(dt), 0));
	End;
End;
ph = sfgf.pivothigh(PivotCount, PivotMap, 0, PivotCount-1, MinStrength);
If ph > 0 Then Begin
	dt = ELDateToDateTime(Date[PivotMap[ph, 0]]);
	CommentaryCL(NewLine, "Highest",
			 	 " Pivot at Bar ", NumToStr(PivotMap[ph, 0], 0),
			 	 " with Strength ", NumToStr(PivotMap[ph, 2], 0),
			 	 " on ", NumToStr(MonthFromDateTime(dt), 0), " / ",
			 	 NumToStr(DayFromDateTime(dt), 0), " / ",
			 	 NumToStr(YearFromDateTime(dt), 0));
End;
pl = sfgf.pivotlow(PivotCount, PivotMap, 0, PivotCount-1, MinStrength);
If pl > 0 Then Begin
	dt = ELDateToDateTime(Date[PivotMap[pl, 0]]);
	CommentaryCL("Lowest",
			 	 " Pivot at Bar ", NumToStr(PivotMap[pl, 0], 0),
			 	 " with Strength ", NumToStr(PivotMap[pl, 2], 0),
			 	 " on ", NumToStr(MonthFromDateTime(dt), 0), " / ",
			 	 NumToStr(DayFromDateTime(dt), 0), " / ",
			 	 NumToStr(YearFromDateTime(dt), 0));
End;

{Paint the Pivots}

PivotScore = 0.0;
For ip = 0 To PivotCount-1 Begin
	If PivotMap[ip, 2] >= MinStrength Then Begin
		pb = PivotMap[ip, 0];
		PHigh = High[pb];
		PLow = Low[pb];
		If PivotMap[ip, 1] = 1 Then
			Color = HighColor;
		If PivotMap[ip, 1] = -1 Then
			Color = LowColor;
		Condition1 = sfgf.drawpaintbar(Date[pb], Time[pb], PHigh, PLow, Color, PBWidth);
		If LastBaronChart Then Begin
			PivotText = Text_New(Date[pb], Time[pb], PHigh, NumToStr(PivotMap[ip, 2], 0));
			Text_SetStyle(PivotText, 2, 1);
			Text_SetColor(PivotText, Color);
		End;
		PivotScore = PivotScore + PivotMap[ip, 1] * PivotMap[ip, 2];
	End;
End;
	
Plot1(PivotScore, "Pivot Score");
Plot2(0, "Zero Line");
Plot3(ScoreAlert, "High Score");
Plot4(-ScoreAlert, "Low Score");
